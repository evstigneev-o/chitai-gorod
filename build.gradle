plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version "8.2.2"
}

repositories {
    mavenCentral()
}

def selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.1",
    allureVersion = "2.21.0",
    jacksonVersion = "2.15.2",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    assertJVersion = "3.24.2",
    ownerVersion = "1.0.4",
    appiumVersion = "8.3.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.assertj:assertj-core:$assertJVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.appium:java-client:$appiumVersion",
    )
}

compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('ui_tests', Test) {
    useJUnitPlatform {
        includeTags("ui")
    }
}

tasks.register('api_tests', Test) {
    useJUnitPlatform {
        includeTags("api")
    }
}

tasks.register('android_tests', Test) {
    useJUnitPlatform {
        includeTags("android")
    }
}